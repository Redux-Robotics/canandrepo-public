plugins {
  id 'cpp'
  id 'java'
  // id 'google-test'
  id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2025.0'
  id 'edu.wpi.first.NativeUtils' version '2025.9.0'
  id 'edu.wpi.first.GradleJni' version '1.1.0'
  id 'edu.wpi.first.GradleVsCode' version '2.1.0'
  id "org.ysb33r.doxygen" version "0.7.0"
}
import org.gradle.internal.os.OperatingSystem

repositories {
  mavenCentral()
  maven {
    url = "https://maven.reduxrobotics.com"
  }
  maven {
    url = uri("../reduxfifo/target/maven")
  }
}
if (project.hasProperty('releaseMode')) {
  wpilibRepositories.addAllReleaseRepositories(project)
} else {
  wpilibRepositories.addAllDevelopmentRepositories(project)
}

// WPILib Version
ext.wpilibVersion = "2026.+"

// Apply C++ configuration
apply from: 'config.gradle'

// Apply Java configuration
dependencies {
    implementation "edu.wpi.first.cscore:cscore-java:$wpilibVersion"
    implementation "edu.wpi.first.cameraserver:cameraserver-java:$wpilibVersion"
    implementation "edu.wpi.first.ntcore:ntcore-java:$wpilibVersion"
    implementation "edu.wpi.first.wpilibj:wpilibj-java:$wpilibVersion"
    implementation "edu.wpi.first.wpiutil:wpiutil-java:$wpilibVersion"
    implementation "edu.wpi.first.wpimath:wpimath-java:$wpilibVersion"
    implementation "edu.wpi.first.wpiunits:wpiunits-java:$wpilibVersion"
    implementation "edu.wpi.first.hal:hal-java:$wpilibVersion"
    implementation "org.ejml:ejml-simple:0.44.0"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.19.2"
    implementation "com.fasterxml.jackson.core:jackson-core:2.19.2"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.19.2"
    implementation 'edu.wpi.first.thirdparty.frc2025.opencv:opencv-java:4.10.0-2'
}

tasks.withType(JavaCompile) {
    // workaround for https://bugs.java.com/bugdatabase/view_bug.do?bug_id=JDK-8278540
    options.compilerArgs.add '-XDstringConcat=inline'
    //options.compilerArgs.add '--enable-preview'
    options.encoding = 'UTF-8'
}

// Set up exports properly
nativeUtils {
  exportsConfigs {
    // Main library is just default empty. This will export everything
    ReduxLib {
      x64ExcludeSymbols = [
        '_CT??_R0?AV_System_error',
        '_CT??_R0?AVexception',
        '_CT??_R0?AVfailure',
        '_CT??_R0?AVruntime_error',
        '_CT??_R0?AVsystem_error',
        '_CTA5?AVfailure',
        '_TI5?AVfailure',
        '_CT??_R0?AVout_of_range',
        '_CTA3?AVout_of_range',
        '_TI3?AVout_of_range',
        '_CT??_R0?AVbad_cast'
      ]
    }
  }
  nativeDependencyContainer {
    ReduxFIFO(getNativeDependencyTypeClass('WPISharedMavenDependency')) {
      version = "2026.0.0-beta1"
      groupId = "com.reduxrobotics.frc"
      artifactId = "ReduxLib-fifo"
      ext = "zip"

      headerClassifier = "headers"
      targetPlatforms = [
        "windowsx86-64",
        "windowsarm64",
        "linuxarm64",
        "linuxx86-64",
        "linuxathena",
        "osxuniversal"
      ]
    }
  }
}
nativeUtils.platformConfigs.named("linuxathena").configure {
  it.cppCompiler.args.add('-fconcepts-diagnostics-depth=42069')
}

// in theory this should check for UB but i don't think it works?
// gradle moment.

//nativeUtils.platformConfigs.named("linuxx86-64").configure {
//  it.cppCompiler.args.add('-g3')
//  it.cppCompiler.args.add('-fsanitize=undefined')
//  it.linker.args.add('-fsanitize=undefined')
//}

model {

  components {
    ReduxLib(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'src/main/native/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/main/native/include'
          }
        }
      }

      nativeUtils.useRequiredLibrary(it, 'wpilib_shared', "ReduxFIFO")
    }
  }
  // testSuites {
  //   ReduxLibTest {
  //       sources.cpp {
  //           source {
  //               srcDir 'src/test/native/cpp'
  //               include '**/*.cpp'
  //           }
  //       }

  //       binaries.all {
  //         lib library: 'ReduxCore', linkage: 'shared'
  //       }

  //       nativeUtils.useRequiredLibrary(it, "wpilib_executable_shared", "googletest_static")
  //   }

  //   ReduxCoreTest {
  //       sources.cpp {
  //           source {
  //               srcDir 'src/test/driver/cpp'
  //               include '**/*.cpp'
  //           }
  //       }

  //       nativeUtils.useRequiredLibrary(it, "wpilib_executable_shared", "googletest_static")
  //   }
  // }
}

apply from: 'publish.gradle'

wrapper {
  gradleVersion = '8.14.3'
}

task listrepos {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
   }
}